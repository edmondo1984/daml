# Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)
load(
    "//bazel_tools:scala.bzl",
    "da_scala_library",
)
load(
    "//language-support/scala/codegen:codegen.bzl",
    "dar_to_scala",
)
load(
    "//daml-lf/language:daml-lf.bzl",
    "lf_dev_version",
    "lf_stable_version",
)
load("@scala_version//:index.bzl", "scala_major_version")

da_scala_library(
    name = "test-common",
    srcs = glob(["src/main/scala/**/*.scala"]),
    visibility = [
        "//visibility:public",
    ],
    deps = [
        "//ledger-api/grpc-definitions:ledger_api_proto_scala",
        "//libs-scala/timer-utils",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:io_grpc_grpc_context",
    ],
)

[
    [
        daml_compile(
            name = "%s-tests%s" % (test_name, target_name),
            srcs = glob(["src/main/daml/%s/*.daml" % test_name]),
            target = target,
            visibility = ["//visibility:public"],
        ),
        dar_to_scala(
            name = "%s-tests%s.scala-codegen" % (test_name, target_name),
            srcs = [":%s-tests%s.dar" % (test_name, target_name)],
            package_prefix = "com.daml.ledger.test.%s" % test_name,
            srcjar_out = "%s%s.scala.srcjar" % (test_name, target_name),
            verbosity = 2,
            visibility = ["//visibility:public"],
        ),
        da_scala_library(
            name = "%s-tests%s.scala" % (test_name, target_name),
            srcs = [":%s-tests%s.scala-codegen" % (test_name, target_name)],
            visibility = ["//visibility:public"],
            deps = [
                "//language-support/scala/bindings",
            ] + extra_deps,
        ) if scala_major_version == "2.12" else None,
    ]
    for (test_name, extra_deps) in [
        (
            "model",
            ["@maven//:org_scala_lang_modules_scala_collection_compat_2_12"],
        ),
        (
            "semantic",
            ["@maven//:org_scala_lang_modules_scala_collection_compat_2_12"],
        ),
        ("performance", []),
    ]
    for (target, target_name) in [
        (lf_stable_version, ""),
        (lf_dev_version, "-dev"),
    ]
]

filegroup(
    name = "dar-files",
    srcs = [
        ":model-tests.dar",
        ":performance-tests.dar",
        ":semantic-tests.dar",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "dev-dar-files",
    srcs = [
        ":model-tests-dev.dar",
        ":performance-tests-dev.dar",
        ":semantic-tests-dev.dar",
    ],
    visibility = ["//visibility:public"],
)
